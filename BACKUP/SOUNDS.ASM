**************************************************************
*
* Software:		?
* Initiated:		
*
* Modified:		Shawn Liptak, 9/30/91	-High speed version
*
* COPYRIGHT (C) 1991 WILLIAMS ELECTRONICS GAMES, INC.
*
*.Last mod - 9/30/91 20:34
**************************************************************
	.FILE	"SOUNDS.ASM"
	.TITLE	"SOUND PROCESSOR"
	.WIDTH	132
	.OPTION	B,D,L,T
	.MNOLIST

; GET THE SYSTEM STUFF

	.INCLUDE	"MPROC.EQU"		;MPROC EQUATES
	.include	"sys.inc"	;z unit system equates
	.include	"macros.hdr"	;macros definitions
	.INCLUDE	"GAME.EQU"		;SOUND HEADER
	.include	"shawn.hdr"		;My macros


;SOUND DRIVER STUFF

	.DEF	SNDRES,ONESND,SNDLD,SNDSND,CKSNDPRI,QSNDRST
	.DEF	MUSICOFF,YAMOFF,FADE_DOWN,ALLOFF,SNDPRC,FADE_UP

;SOUND HEADERS USED IN GAME

	.DEF	UGH1,UGH2,UGH3,UGH4,MEUGH,HASND,NOWAY,BUYMSC,GUYDI,MTING
	.DEF	TRGTSND,KEYSND,CHUCK,HELP,HELP2,HELP3,HELP4,KEYSND2,WRPSND
	.DEF	ALTEXP5,EXP5,GULP,HORN2,TORCH,ALTEXP6,SCASH2,YEAHHH,CNTDWN2
	.DEF	SALTXP4,SEXP2HI,HORNSND,EXITSND,LOVEIT,CRSSND,CNTDOWN
	.DEF	TING2,TING3
	.DEF	ALTEXP1,ALTEXP3,EXP2HI,SETDWN,GRNTSHT,GETWPN,AIRSCRM,EXP1L
	.DEF	ALTXP1A,ALTEXP4,CANNON,EXP2HIB,TUNE3,TUNE3O,CRSSND2,SQUISH
	.DEF	SHOT,THUMP2,THUMP,EXP1,EXP2,EXP3,EXP4,TOTCARN,FIREDI,GETSND
	.DEF	ELECTRO,MECH1,GLSEXP,TUNE1,TUNE2,SENDIN,WOO1,WOO2,COINSND
	.DEF	FEET,FLAMSND,SHOT2,STATSND,JEEPSND,TURTSND,FNDSND
	.DEF	DIESCRM,SCREAM,SLIDEIN,PLYRONE,PLYRTWO,AGLSEXP
	.DEF	TING,GRNSND,BEEP1,THNKU,SCASH,FREEMAN,SCASH3
	.DEF	HIMUSC,BEEP2


	.REF	SOUNDSUP

	.text

*SOUND PROCESSOR MAKES SOUNDS FOR N-CHANNELS (CURRENTLY 6):
*
*		CHANNEL #0	OKI CHANNEL 3
*		CHANNEL #1	OKI CHANNEL 4
*		CHANNEL #2	YAMAHA SOUNDS
*		CHANNEL #3	MUSIC	      
*		CHANNEL #4	OKI CHANNEL 1 (PLAYER 1)
*		CHANNEL #5	OKI CHANNEL 2 (PLAYER 2)
*
*THE ROUTINE "SNDRES" MUST BE CALLED TO INITAILIZE SOUND BOARD
*ALSO SCRATCHPAD RAM MUST BE INITIALIZED TO ZERO
*THE ROUTINE "SNDPRC" MUST BE CALLED EVERY 16MSEC. IN THE MAIN PROGRAM LOOP
*(THE SAME PLACE WHERE "PRCDSP" IS NORMALLY CALLED)
*MAKING A SOUND:
*
*SOUND TABLE FORMAT
*
*SOUND DESCRIPTION:
*	.WORD PRIORITY, TIME, SOUND CODE, (PRIORITY), (TIME), (SOUND CODE), 0
*
*SOUND ENDS IN 0000.
*PRIORITY WORD = 1111INNNPPPPPPPP; I=1=NON-INTERRUPTABLE
*NNN=CHANNEL #(0-7);
*PP=PRIORITY (00-FF), FF=HIGHEST.
*TIME WORD =TTTT (0000-7FFF X 16MS).
*SOUND WORD =8SSS SSS=SOUND CODE(000-FFF).
*EXAMPLE 1:
*
*	MOVI	SND1,A0		;LOAD ABSOLUTE ADDRESS OF SND1 IN A0
*	CALLA	ONESND		;MAKE ONE SND1
*
*SND1:	.WORD	0F0FFH,00100H,08088H,0	
*
*SND1 IS FOR CHANNEL=0, PRIORITY=FF (HIGHEST), TIME = 100H X 16MSEC.
*SOUND CODE = 88H
*SND1 WILL MAKE ONE SOUND 88H, AND NOT ALLOW IT TO BE INTERRUPTED 
*FOR 100H X 16MSEC., EXCEPT BY A SOUND OF EQUAL OR GREATER PRIORITY ON
*CHANNEL 0
*
*	MOVI	SND2,A0		;LOAD SND2 ADDRESS
*	MOVK	3,A1		;REPEAT IT 3 TIMES
*	CALLA	SNDLD		;GO DO IT

*SND2:	.WORD	0F311H,00020H,08044H,08044H,0
*
*SND2 IS ON CHANNEL 3, PRIORITY 11H. IT WILL MAKE ONE SOUND 44H, WAIT
*20H X 16 MSEC., THEN MAKE ANOTHER SOUND 44H, WHICH CANNOT BE INTERRUPTED
*FOR 20H X 16 MSEC., EXCEPT BY A SOUND OF EQUAL OR GREATER PRIORITY ON
*CHANNEL 3
*
*SINCE A1 IS LOADED WITH A REPEAT COUNT OF 3, THIS PROCESS WILL BE REPEATED
*THREE TIMES. THE END RESULT WILL BE 6 SOUND 44H SEPERATED BY 20H X 16MSEC.
*
*NOTE: THE CHANNEL SPECIFIED BY "MUSICHAN" WILL CAUSE THE MUSIC SECTION OF
*	THE SOUND BOARD TO BE STROBED.
*
SNDPRI	.SET	0  	;PRIORITY (00=LOWEST, FF=HIGHEST)
SNDTMR	.SET	010h	;TIMER COUNTDOWN
SNDPTR	.SET	020h	;POINTER TO SOUND TABLE DATA
SNDREP	.SET	040h	;REPEAT COUNT OF SOUND
;SNDTIM	.SET	050h	;TIMER 000-7FFF
SNDST	.SET	060h	;ADDRESS OF SOUND TABLE START
*NOTE: IF YOU CHANGE SNDSIZ, MAKE SURE ANY CODE THAT USES IT AS A DIVISOR 
*	PERFORMS THE CORRECT OP. CURRENTLY IT IS A POWER OF 2 SO SRL IS USED.
SNDSIZ	.SET	080h

SL_CHANNEL	.EQU	21	;SHIFTS REQUIRED TO ISOLATE CHANNEL NUMBER
SR_CHANNEL	.EQU	29

NCHAN		EQU	6	;DECLARE NUMBER OF ALLOWED CHANNELS
MUSICHAN	EQU	3	;THIS CHANNEL IS FOR THE TUNES

*THESE STROBE EQUATES ARE FOR THE HOT NEW WILLY VIDEO SOUND CARD tm
RESETBIT	EQU	0FE00H	;THIS IS THE ^RESET BIT
SOUNDBIT	EQU	0FD00H	;THIS IS THE SOUND SIDE STROBE

EXTENDED_CODE	.EQU	7AH	;SEND THIS TO INDICATE EXTENDED SOUND CALL

*
*EQUATES FOR READING SOUND BOARD IRQ REQUEST LINE
*
TALKPORT	.EQU	SWITCH+10H	;PORT TO FIND THIS LINE
B_SIRQ		.EQU	14		;BIT TO READ FOR SOUND IRQ LINE

	.BSS	SNDSTR,NCHAN*SNDSIZ	;RESERVE STORAGE AREA
	.BSS	SNDEND,0		;END OF SOUND PROCESSOR RAM

**************************************************************************
*                                                                        *
* CLRSNDDB - CLEAR THE SOUND PROCESSOR DATA BASE			 *
*                                                                        *
**************************************************************************
CLRSNDDB
	MMTM	SP,A0,A1
	MOVI	SNDSTR,A1
	CLR	A0
CLRSDBL
	MOVE	A0,*A1+
	CMPI	SNDEND,A1
	JRLO	CLRSDBL
	MMFM	SP,A0,A1
	RETS

**************************************************************************
* Make one sound
* A0=* to sound data
* Rets: A0=Same

ONESND
	PUSH	a0,a1,a2,a3,a4
	movk	1,a1
	jruc	sl20

********************************
* Sound loader
* A0=* to sound data, A1=REPEAT COUNT
*Sound format= PRI,TIME,SOUND CODE,(PRI),(TIME),(SOUND CODE),0=END
*PRIORITY WORD = 1111INNNPPPPPPPP; I=1=NON-INTERRUPTABLE
* NNN=CHANNEL #(0-7); PP=PRIORITY (00-FF), FF=HIGHEST.
*TIME WORD =TTTT (0000-7FFF X 16MS).
*SOUND WORD =8SSS SSS=SOUND CODE(000-FFF).

SNDLD
	PUSH	a0,a1,a2,a3,a4

sl20	move	@SOUNDSUP,a2
	jrnz	sndldx			;Somebody says no?

	move	*a0,a2			;Get priority word
	move	a2,a3
	sll	SL_CHANNEL,a2
	srl	SR_CHANNEL,a2		;Extract channel bits
	sll	7,a2			;*SNDSIZ
	addi	SNDSTR,a2 		;+Base

	move	*a2,a4			;Get current priority
	btst	11,a4
	jrnz	sndldx			;Non-interruptable?
	sll	32-8,a3			;Mask priorities
	sll	32-8,a4
	cmp	a4,a3
	jrlo	sndldx			;New pri < old pri?

	move	a0,*a2(SNDST),L 	;Setup sound start pointer
	move	*a0+,*a2+,L		;Set PRI,TMR
	move	*a0+,a3			;Snd code
	move	a0,*a2+,L		;Set PTR
	move	a1,*a2+			;Set RPT (Repeat count)
	callr	SNDSND			;Send sound code

sndldx	PULL	a0,a1,a2,a3,a4
	rets


********************************
* Sound processor called every main loop (16 MSEC)

SNDPRC
	movi	SNDSTR,a0
	movk	NCHAN,a1
splp	move	*a0(SNDTMR),a2 	;Check timer
	jrz	sp5		;Inactive channel?
	subk	1,a2
	move	a2,*a0(SNDTMR)	;New time
	jrnz	sp5		;No timeout?
	callr	SNDUPD		;Update sound
sp5	addi	SNDSIZ,a0
	dsjs	a1,splp
	rets


********************************
* Update sound
* A0=* to sound channel ram

SNDUPD
        move	*a0(SNDPTR),a2,L	;Get * to rom table
sud2	move	*a2+,a3			;Get next rom table entry
	jrnz	sud8			;Another snd?

	move	*a0(SNDREP),a3		;Check repeat count
	subk	1,a3
	move	a3,*a0(SNDREP)
	jrle	sud6			;No repeats?
	move	*a0(SNDST),a2,L
	jruc	sud2			;Start sound over

sud6	clr	a2
	move	a2,*a0			;Clear sound priority
	move	a2,*a0(SNDST),L		;Clear sound ptr 
	rets

sud8	move	a3,*a0+			;Set PRI
	move	*a2+,*a0+		;Set TMR
	move	*a2+,a3			;Get code
	move	a2,*a0,L		;Set PTR
	subk	32,a0			;Fix A0
	jruc	SNDSND			;Send sound code



**************************************************************************
* SNDSND - RAW HARDWARE SOUND CALL					 *
* A3=SOUND CODE B0-B7							 *
* NOTE: TRASHES A14							 *
**************************************************************************
SNDSND
	PUSH	a0,a3

	SLL	20,A3
	SRL	20,A3		;STRIP OFF BITS

	CMPI	100H,A3		;IS THIS AN EXTENDED SOUND CALL?
	JRLO	SNDSND_BASE	;BR = NO
	MOVE	A3,A0
	MOVI	EXTENDED_CODE,A3	;SEND THE EXTENDED FEATURE
	CALLR	SNDSND
	MOVE	A0,A3
	SLL	32-8,A3
	SRL	32-8,A3
SNDSND_BASE

	ADDI	SOUNDBIT,A3	;PULL SOUND SECTION INTERRUPT
	MOVE	A3,@SOUND	;HIT SOUND

	MOVK	8,A0
	DSJS	A0,$		;SLOW IT DOWN A BIT

	ORI	0FF00h,A3	;PUT OUT 1'S
	MOVE	A3,@SOUND

	MOVI	8,A0
	DSJS	A0,$		;LET SOUND BOARD REACT

	MOVB	@TALKPORT+B_SIRQ-7,A0		;ARE WE CONNECTED?
	JRN	SS_DEFAULT_WAIT			;BR = NO

****	MOVI	390,A0		;MAX FIRQ DELAY ON SOUND BOARD IS 180 micros
****	DSJS	A0,$

	MOVI	390,A3		;MAX IRQ DELAY FOR SOUND BOARD
SS_IRQ_WAIT
	MOVB	@TALKPORT+B_SIRQ-7,A0		;DID SOUND BOARD SERVICE?
	JRN	SS_X				;BR = YES
	DSJS	A3,SS_IRQ_WAIT
SS_X
	PULL	a0,a3
	RETS	

*COME HERE BECAUSE SOUND BOARD IRQ REQUEST WAS NOT DETECTED
SS_DEFAULT_WAIT
	MOVI	390,A0		;MAX IRQ DELAY FOR SOUND BOARD
	DSJS	A0,$
	PULL	a0,a3
	RETS	

**************************************************************************
*								         *
* SNDRES - FULL SOUND BOARD RESET, SOUND CHECKSUMS ARE REPORTED.	 *
* 									 *
* NOTE: TRASHES A14							 *
*								         *
**************************************************************************
SNDRES
	PUSH	a0
	MOVI	RESETBIT,A0	;HIT RESET BIT
	MOVE	A0,@SOUND
	MOVI	100,A0		;WAIT FOR IT TO CATCH
	DSJS	A0,$	
	movi	-1,a0		;Let it go
	MOVE	A0,@SOUND
	PULL	a0
	RETS

**************************************************************************
*								         *
* IS_SOUND - CHECK SOUND DATA BASE TO SEE IF THE GIVEN SOUND SCRIPT	 *
* 		IS CURRENTLY BEING PROCESSED. (I.E. ANOTHER SOUND	 *
* 		HAS OR HAS NOT TAKEN OVER.)				 *
* A0 = PTR TO SOUND SCRIPT						 *
* RETURNS								 *
* 	 Z = SAME SCRIPT						 *
* 	NZ = SCRIPT NO LONGER ACTIVE					 *
*								         *
**************************************************************************
IS_SOUND		
	PUSH	A3
	MOVE	*A0,A14,W	;GET CHANNEL/PRIORITY WORD
	MOVI	SNDSIZ,A3
	SLL	SL_CHANNEL,A14
	SRL	SR_CHANNEL,A14	;EXTRACT CHANNEL BITS
	MPYU	A14,A3
	ADDI	SNDSTR,A3 	;GET THE CORRECT RAM CHANNEL ADDRESS
	MOVE	*A3(SNDST),A14,L
	CMP	A0,A14		;SAME SCRIPT ACTIVE?
	PULL	A3
	RETS

**************************************************************************
*                                                                        *
* CKSNDPRI - CHECK SOUND CHANNEL FOR EXISTANCE OF A GIVEN SOUND PRIORITY *
* A0 = CHANNEL #							 *
* A1 = SOUND PRIORITY							 *
* RETURNS:								 *
* CARRY CLEAR = PRIORITY NOT ACTIVE					 *
* CARRY SET = PRIORITY ACTIVE						 *
*                                                                        *
**************************************************************************
CKSNDPRI
	MMTM	SP,A0,A2
	MOVI	SNDSTR,A2
	SLL	7,A0			;CHANNEL x 128
	ADD	A2,A0			;OFFSET TO CORRECT CHANNEL AREA
	MOVB	*A0,A0
	CMP	A0,A1
	JRNE	CKSPRI1			;BR = PRIORITY NOT ACTIVE
	SETC
	MMFM	SP,A0,A2
	RETS
CKSPRI1
	CLRC
	MMFM	SP,A0,A2
	RETS

**************************************************************************
*                                                                        *
* QSNDRST - QUIET SOUND RESET, NO DIAGNOSTIC DONGERS			 *
*                                                                        *
**************************************************************************
QSNDRST
	MMTM	SP,A3,A5
	CALLR	SNDRES		;RESET THE BOARD
	MOVI	100,A0
	DSJS	A0,$		;NEED 30 mics BEFORE SENDING CODE
	CLR	A3
	CLR	A5
	CALLR	SNDSND		;PEND ON DIG SIDE
	MOVK	MUSICHAN,A5
	CALLR	SNDSND		;PEND ON YAMAHA SIDE	
	MMFM	SP,A3,A5
	RETS

**************************************************************************
*                                                                        *
* FADE_UP - PROCESS TO FADE THE SOUND UP				 *
* A11 = FADE RATE							 *
*                                                                        *
**************************************************************************
FADE_UP:
	MOVI	FADE_UP_TAB,A8
	JRUC	FADER
**************************************************************************
*                                                                        *
* FADE_DOWN - PROCESS TO FADE THE SOUND UP				 *
* A11 = FADE RATE							 *
*                                                                        *
**************************************************************************
FADE_DOWN:
	MOVI	FADE_DOWN_TAB,A8
	JRUC	FADER

FADER:
	MOVI	FADEPID,A0
	CLR	A1
	NOT	A1
	CALLA	KILALL			;WASTE ANY OTHER FADERS
FADE_LP:
	MOVE	*A8+,A0,L
	JRZ	FADE_DONE
	CALLR	ONESND
	SLOOPR	A11,FADE_LP
FADE_DONE:
	DIE

FADE_UP_TAB:
	.LONG	VOLUME0
	.LONG	VOLUME1
	.LONG	VOLUME2
	.LONG	VOLUME3
	.LONG	VOLUME4
	.LONG	VOLUME5
	.LONG	VOLUME6
	.LONG	VOLUME7
	.LONG	VOLUME8
	.LONG	VOLUME9
	.LONG	VOLUMEA
	.LONG	VOLUMEB
	.LONG	VOLUMEC
	.LONG	VOLUMED
	.LONG	VOLUMEE
	.LONG	VOLUMEF
	.LONG	0

FADE_DOWN_TAB:
	.LONG	VOLUMEF
	.LONG	VOLUMEE
	.LONG	VOLUMED
	.LONG	VOLUMEC
	.LONG	VOLUMEB
	.LONG	VOLUMEA
	.LONG	VOLUME9
	.LONG	VOLUME8
	.LONG	VOLUME7
	.LONG	VOLUME6
	.LONG	VOLUME5
	.LONG	VOLUME4
	.LONG	VOLUME3
	.LONG	VOLUME2
	.LONG	VOLUME1
	.LONG	VOLUME0
	.LONG	0

**************************************************************************
*                                                                        *
* 		POPULAR SOUND CALLS 					 *
*                                                                        *
**************************************************************************
*
*HIGH PRIORITY OFF CODES, NOTHING SHOULD BEAT THESE
CVSDOFF  .WORD	0F0FFH|0800H,001H,08000H,0		;CVSD BACKGROUND LOOP OFF
CVSDFOFF .WORD	0F0FFH|0800H,001H,08000H,0		;CVSD FOREGROUND LOOP OFF
SOUNDOFF .WORD	0F0FFH|0800H,001H,08000H,0		;TURN SOUND SIDE OFF
ALLOFF	 .WORD	0F3FFH|0800H,001H,08000H,0		;MUSIC AND EFFECTS OFF
MUSICOFF .WORD	0F3FFH|0800H,001H,0807EH,0		;TURN JUST MUSIC OFF
MUSITOFF .WORD	0F3FFH|0800H,001H,08040H,0		;MUSIC TRANSITION OFF
YAMOFF	 .WORD	0F2FFH|0800H,001H,0807FH,0		;YAMAHA EFFECT OFF

VOLUME0	.WORD	0F3FEH,01H,0802FH,0
VOLUME1	.WORD	0F3FEH,01H,0802EH,0
VOLUME2	.WORD	0F3FEH,01H,0802DH,0
VOLUME3	.WORD	0F3FEH,01H,0802CH,0
VOLUME4	.WORD	0F3FEH,01H,0802BH,0
VOLUME5	.WORD	0F3FEH,01H,0802AH,0
VOLUME6	.WORD	0F3FEH,01H,08029H,0
VOLUME7	.WORD	0F3FEH,01H,08028H,0
VOLUME8	.WORD	0F3FEH,01H,08027H,0
VOLUME9	.WORD	0F3FEH,01H,08026H,0
VOLUMEA	.WORD	0F3FEH,01H,08025H,0
VOLUMEB	.WORD	0F3FEH,01H,08024H,0
VOLUMEC	.WORD	0F3FEH,01H,08023H,0
VOLUMED	.WORD	0F3FEH,01H,08022H,0
VOLUMEE	.WORD	0F3FEH,01H,08021H,0
VOLUMEF	.WORD	0F3FEH,01H,08020H,0

*
*SOUND PROCESSOR MAKES SOUNDS FOR N-CHANNELS (CURRENTLY 6):
*
*		CHANNEL #0	OKI CHANNEL 3 MISC. SAMPLES
*		CHANNEL #1	OKI CHANNEL 4 SPEECH
*		CHANNEL #2	YAMAHA SOUNDS FX
*		CHANNEL #3	BACKGROUND MUSIC
*		CHANNEL #4	OKI CHANNEL 1 EXPLOSIONS
*		CHANNEL #5	OKI CHANNEL 2 GUN SHOTS
*
*PRIORITY WORD = 1111INNNPPPPPPPP; I=1=NON-INTERRUPTABLE
*NNN=CHANNEL #(0-7);
*PP=PRIORITY (00-FF), FF=HIGHEST.
*TIME WORD =TTTT (0000-7FFF X 16MS).
*SOUND WORD =8SSS SSS=SOUND CODE(000-FFF).


TUNE1	.WORD	0F3FEH,10,08001H,0		;REPORTER TUNE AT INTRO
TUNE2	.WORD	0F3FEH,10,08002H,0		;INTRO INTO DESERT
TUNE3	.WORD	0F3F0H,10,08005H,0		;INTRO INTO TAUNTS/RACKUP
TUNE3O	.WORD	0F3F0H,10,08090H,0		;TUNE 3 OFF
HIMUSC	.WORD	0F3F0H,10,08001H,0		;HIGH SCORE MUSIC
BUYMSC	.WORD	0F3FEH,10,08006H,0		;BUYIN MUSIC
SHOT	.WORD	0F450H,05,08043H,0		;REGULAR SHOT LOWER VOLUME
THUMP	.WORD	0F460H,08,08046H,0		;NON STOP THUMPER SOUND
CANNON	.WORD	0F560H,09,080DEH,0		;T72 CANNON
THUMP2	.WORD	0FC58H,04,08046H,0		;SPDBALL THUMPER SOUND #2
EXP1	.WORD	0FC55H,18,08030H,0		;EXPLOSION
EXP1L	.WORD	0FC55H,10,08040H,0		;GRUNT EXP SND
EXP2	.WORD	0FC55H,10,08032H,0		;EXPLOSION LARGE
EXP2HI	.WORD	0FC75H,10,08032H,0		;EXPLOSION LARGE HIGHER PRI
EXP2HIB	.WORD	0FC75H,05,08032H,0		;EXPLOSION LARGE HIGHER PRI
EXP3	.WORD	0F455H,06,08034H,0		;EXPLOSION
EXP4	.WORD	0FC5AH,06,0803AH,0		;SMART BOMB SND
EXP5	.WORD	0FC7AH,06,08038H,0		;
SEXP2HI	.WORD	0F4E5H,35,08032H,0		;EXPLOSION LARGE HIGHER PRI
SALTXP4 .WORD	0F5E5H,20,080D8H,0
ALTEXP1	.WORD	0FD75H,10,080E0H,0		;GRUNT EXP SND
ALTXP1A	.WORD	0FD85H,04,080D4H,0  		;ALT CHANNEL EXPLOSION
ALTEXP3	.WORD	0F575H,15,080D9H,0		;ALT CHAN MINE LAYER EXP
ALTEXP4	.WORD	0F575H,010H,080DFH,0		;ALT CHAN EXP - JEEPS
ALTEXP5	.WORD	0F575H,010H,080DEH,0		;ALT CHAN EXP
ALTEXP6	.WORD	0F577H,038H,080DEH,0		;ALT CHAN EXP
TORCH	.WORD	0FDB5H,40,080BCH,0 		;AHMED FLAME THROWER
SETDWN	.WORD	0F8F4H,05,080D4H,0		;SET DOWN TIME BOMB
CNTDOWN	.WORD	0FBF5H,05,080A4H,0		;BUYIN CNTDWN CLANG
CNTDWN2	.WORD	0FBF5H,05,080A5H,0		;BUYIN CNTDWN CLANG #2
FLAMSND	.WORD	0F460H,05,08044H,0		;FLAME THROWER
SHOT2	.WORD	0FC61H,05,0804EH,0		;SPRAY WEAPON
GRNTSHT	.WORD	0F54FH,10,080E7H,0		;GRUNT SHOOTS AT PLYR
TURTSND	.WORD	0F550H,08,0805CH,0		;TURRET GUN FIRE SND
TOTCARN	.WORD	0F9F0H,100,080CFH,0		;"TOTAL CARNAGE"
SENDIN	.WORD	0F180H,01,080CEH,0    		;"SEND IN SQUAD"
ELECTRO	.WORD	0F0F0H,40,080C9H,0		;SHOCK SND AT DEATH
MECH1	.WORD	0F080H,40,080C5H,0		;MECHANICAL SND 1
AGLSEXP	.WORD	0FDE8H,20,080DBH,0		;ALT CHAN GLASS SHATTER
GLSEXP	.WORD	0FCE8H,20,08036H,0		;GLASS SHATTER
WOO1	.WORD	0F97EH,024H,080CBH,0		;WHHOOO
WOO2	.WORD	0F97EH,024H,080CCH,0		;"YEAH"
THNKU	.WORD	0F97EH,010H,080BEH,0		;"THANK YOU"
SCASH3	.WORD	0F395H,08,08091H,0		;HOSTAGE PICKUP
HELP	.WORD	0F97EH,024H,080C1H,0		;"HELP"
HELP2	.WORD	0F97FH,034H,080BFH,0		;"OVER HERE"
HELP3	.WORD	0F97EH,024H,080F1H,0		;WHISTLE
HELP4	.WORD	0F87EH,024H,080D6H,0		;WHISTLE
GUYDI	.WORD	0F9FDH,044H,080FFH,0		;BAD GUY DIE SCREAM
YEAHHH	.WORD	0F9F0H,064H,080FEH,0		;RACKUP YEAHHH
SCASH	.WORD	0F395H,08,0808CH,0		;CASH PICKUP SND
SCASH2	.WORD	0F395H,08,080A1H,0		;BMB PICKUP SND
SQUISH	.WORD	0FBA5H,18,080ADH,0		;SQUISH SND
WRPSND	.WORD	0FBFEH,38,080A9H,0		;WARP SND
GETWPN	.WORD	0F858H,15,080D5H,0		;GUN BEING COCKED
COINSND	.WORD	0FBFFH,010H,08098H,0		;COIN IN MELODY SND
NOWAY	.WORD	0F9F7H,040H,080E6H,0		;"NO WAY" AT GAME OVER
FREEMAN	.WORD	0F1EEH,018H,080CDH,0		;FREE MAN SPEECH
KEYSND	.WORD	0F3EEH,018H,080A8H,0		;KEY MELODY SND
KEYSND2	.WORD	0FBEEH,018H,08098H,0		;KEY MELODY SND
GETSND	.WORD	0FBEEH,018H,080ABH,0		;GET HELPER
LOVEIT	.WORD	0F9F0H,078H,080CDH,0		;"I LOVE IT"
FEET	.WORD	0F06EH,018H,080CAH,0		;FAST FEET PICKUP
GRNSND	.WORD	0F365H,018H,08089H,0		;GRENADE LAUNCH SND
STATSND	.WORD	0F08EH,018H,080C0H,0		;STATIC
JEEPSND	.WORD	0F08FH,018H,080C3H,0		;JEEP DOWN SCRN
HORNSND	.WORD	0F38EH,018H,08087H,0		;JEEP HORN 1
HORN2	.WORD	0F88EH,018H,080E9H,0		;JEEP HORN 2
EXITSND	.WORD	0F38EH,018H,0808CH,0		;EXIT SND
FNDSND	.WORD	0F99EH,018H,080D2H,0		;FIEND DEATH
BEEP1	.WORD	0F3F7H,08H,080ACH,0		;BEEP CLICK
BEEP2	.WORD	0F3F7H,030H,08096H,0		;BEEP TONE 2 - NO BMB
TRGTSND	.WORD	0FBF0H,050H,08097H,0		;CRUISE MISSILE TARGETED
PLYRONE	.WORD	0F9FEH,058H,080D0H,0		;"PLAYER ONE"
PLYRTWO	.WORD	0F9FEH,058H,080D1H,0		;"PLAYER TWO"
DIESCRM	.WORD	0F18EH,08H,080D2H,0		;DIE SCREAM BEFORE SHOCK SND
FIREDI	.WORD	0F88EH,040H,080D7H,0		;BURNING FLAME FOR DEATH
AIRSCRM	.WORD	0F9FDH,058H,080D3H,0		;DIE SCREAM FOR UP INTO AIR
SCREAM	.WORD	0F1F2H,048H,080E8H,0		;DIE SCREAM FOR CANDY BLITZER
SLIDEIN	.WORD	0F3F7H,08H,0808EH,0		;SLIDE IN SOUND
TING	.WORD	0F38BH,010H,0809CH,0		;BULLET HITTING METAL
TING2	.WORD	0F38BH,010H,0809DH,0		;BULLET HITTING METAL
TING3	.WORD	0F38BH,010H,0809EH,0		;BULLET HITTING METAL
MTING	.WORD	0F3BBH,05H,080A3H,0		;LAND MINE BLOW UP
GULP	.WORD	0FB8BH,035H,0809FH,0		;SHIELD ON
CRSSND	.WORD	0FCEBH,035H,080EAH,0		;CRUISE MISSILE COMING IN
CRSSND2	.WORD	0F4EBH,035H,080EAH,0		;CRUISE MISSILE COMING IN
MEUGH	.WORD	0F9F0H,15,080FDH,0		;PLAYER DIE UGH
UGH1	.WORD	0F1A0H,8,080F5H,0	     ;C0	;UGH
UGH2	.WORD	0F1A0H,8,080F8H,0			;UGH
UGH3	.WORD	0F1A0H,8,080FAH,0			;UGH
UGH4	.WORD	0F1A0H,8,080FCH,0			;UGH
HASND	.WORD	0F9F2H,030H,080F2H,0		;WICKED LAUGHTER
CHUCK	.WORD	0F0F3H,070H,080C7H,0		;UPCHUCK SND FOR FIENDS


;WOO3	.WORD	0F1FEH,034H,080F0H,0		;GIRL YEAH
;VICMSC	.WORD	0F2F0H,100,080F0H,0		;MUTOID VICTORY MUSIC
;STRYMSC	.WORD	0F280H,300,080F0H,0		;MUSIC FOR STORY IN DEMO
;ENDTUNE	.WORD	0F280H,300,080F0H,0		;END OF GAME TUNE
;DOOR1	.WORD	0F376H,5,080F0H,0			;DOOR SLIDING OPEN
;DOOR2	.WORD	0F376H,5,080F0H,0			;DOOR SLIDING SHUT
;FSHOT	.WORD	0F7A0H,02EH,080F0H,0		;POD SIZZLE
;TUNE4	.WORD	0F2FFH,120,080F0H,0		;3RD BKRND TUNE (SNAKES)
;BTUNE1	.WORD	0F2FFH,0120H,080F0H,0		;MUTOID BOSS TUNE
;BTUNE2	.WORD	0F2FFH,0120H,080F0H,0		;FACE BOSS TUNE
;BTUNE3	.WORD	0F2FFH,0120H,080F0H,0		;SNAKE BOSS TUNE
;ICON	.WORD	0F3AFH,01EH,080F0H,0		;WEAPON ICON PICKUP
;GDTH2	.WORD	0F3AFH,01EH,080F0H,0		;GRUNT DEATH
;RIFL	.WORD	0F2E8H,008H,080F0H,0		;RIFLE SPRAY WEAPON
;HEXP	.WORD	0F3A0H,010H,080F0H,0		;OTHER HULK DEATH
;HEXP2	.WORD	0F3A0H,01EH,080F0H,0		;OTHER HULK DEATH
;HEXP3	.WORD	0F3A0H,01EH,080F0H,0		;OTHER HULK DEATH
;BUBL	.WORD	0FBFBH,01EH,080F0H,0		;BLOOD BUBBLE
;LOWSND	.WORD	0FBFDH,120,080F0H,0		;WEAPON LOW SND
;LAZSND	.WORD	0F3F7H,010H,080F0H,0		;LAZER BOLTS FROM EYES
;BONS1	.WORD	0F3FFH,01H,080F0H,0		;BONUS BUILDUP
;BONS2	.WORD	0F3FEH,01H,080F0H,0		;BONUS END
;SCRM	.WORD	0F3EFH,010H,080F0H,0		;SNAKE SCREAM DEATH
;BEEP3	.WORD	0F3F7H,08H,080F0H,0		;BONUS PRIZE ERASE
;MONIN	.WORD	0F3F8H,010H,080F0H,0		;MONITOR SLIDE IN
;MISS	.WORD	0F2E8H,008H,080F0H,0		;MISSLES
;MISS2	.WORD	0F0E7H,008H,080F0H,0		;MISSLES FROM BAD GUYS
;BLDS	.WORD	0F3DFH,01EH,080F0H,0		;BLADES
;TING2	.WORD	0F3F3H,00FH,080F0H,0		;BULLET HITTING WALL
;GIFT	.WORD	0F3A0H,50,080F0H,0		;PRESENT PICKUP
;GDTH1	.WORD	0F3E4H,35,080F0H,0		;GRUNT DEATH
;EXP1	.WORD	0F0E8H,008H,080F0H,0		;EXPLOSION SMALL
;EXP3	.WORD	0F0E8H,00FH,080F0H,0		;EXOLOSION LARGE
;EXP3HI	.WORD	0F2FEH,15,080F0H,0		;EXOLOSION LARGE FOR SMRT BMB
;EXP3HIA	.WORD	0F8FFH,35,080F0H,0		;EXOLOSION LARGE FOR MUTOID
;EXP3HIB	.WORD	0F0FFH-08,35,080F0H,0		;EXOLOSION LARGE FOR SMRTBMB
;UGH	.WORD	0F8FEH,15,080F0H,0		;PLAYER DIE SCREAM
;SHKSND	.WORD	0F8FEH,120,080F0H,0		;PLAYER ELECTRIC SHOCK
;CROWD1	.WORD	0F8F8H,04EH,080F0H,0		;CROWD NOISE CHEERS
;CROWD1A	.WORD	0F0F8H,03EH,080F0H,0		;CROWD NOISE CHEERS TVMNTR
;CROWD2	.WORD	0F8F8H,02EH,080F0H,0		;CROWD NOISE OOHS N AAHS
;ICSND	.WORD	0F3EFH,01EH,080F0H,0		;ICON PICKUP
;
;SPEECH TABLES
;
;PZLSND	.LONG	PZ1SND,PZ2SND,PZ3SND
;PZ1SND	.LONG	WOO3,WOO1,WOO1	;STSTR,SCAR,SVCR
;PZ2SND	.LONG	WOO3,WOO1,WOO1	;STELE,SLUGG,STRIP
;PZ3SND	.LONG	WOO3,WOO1,WOO1	;SHGM,SMWR,SMEAT

;
;SPEECH
;
;GOGO	.WORD	0F2FEH,018H,080F0H,0		;GIRLS 'GO!'
;UGH2	.WORD	0F2E8H,014H,080F0H,0		;UGH! / SNAKE GRUNT
;UGH3	.WORD	0F2E8H,014H,080F0H,0		;OOF!
;

;OUCH	.WORD	0F2FEH,040H,080F0H,0		;OUCH GROWL
;AWWSND	.WORD	0F2FDH,03AH,080F0H,0		;BIG AWW SND
;AWWSND2	.WORD	0F2FFH,03AH,080F0H,0		;BIG AWW SND FLAT FACE
;EESND	.WORD	0F2FDH,030H,080F0H,0		;EE-AAH	SND
;ONOSND	.WORD	0F2FEH,030H,080F0H,0		;OH NO SND
;HA2SND	.WORD	0F2FEH,030H,080F0H,0		;HAHA SND
;WPWPSND	.WORD	0F2FFH,040H,080F0H,0		;WHOOP WHOOP SND
;WPSSND	.WORD	0F2FFH,040H,080F0H,0		;LOTS OF WHOOPS

;

;BINGO	.WORD	0F2FFH-07,030H,080F0H,0		;"BINGO"
;MCSPK2	.WORD	0F2FEH,03FH,080F0H,0		;BIG MONEY
;MCSPK3	.WORD	0F2FEH,03FH,080F0H,0		;BIG PRIZES
;MCSPK5	.WORD	0F8FFH-06,05BH,080F0H,0		;CONTESTANT 1
;MCSPK6	.WORD	0F8FFH-06,05BH,080F0H,0		;CONTESTANT 2
;MCSPK7	.WORD	0F8FEH,053H,080F0H,0		;"TOTAL CARNAGE"
;LEGOSND	.WORD	0F8FEH,030H,080F0H,0		;LET'S GO
;MCSPK9	.WORD	0F2FFH-07,053H,080F0H,0		;TEN SECONDS
;MCSPK10	.WORD	0F8FEH,043H,080F0H,0		;"YOU'LL NEED IT"
;MCSPK11	.WORD	0F8FEH,043H,080F0H,0		;"BUY FOR A DOLLAR:
;MCSPK4	.WORD	0F2FEH,03FH,080F0H,0		;"GOOD LUCK"


	.END
